################################################################################
#               BootManager 2.x Configuration Manager v1.5                     #
################################################################################

# Modification: 
# R3Z3N has modified this to hide advanced config if pin code is not 0000.
# HDD and ATAD menu widgets hidden for v14+. Megamemory Patcher hidded for v12+
# Show Directory, Confirm remove/install, Tutorials added

KEEP

SET "BM.CONFMENU_LOADED" "1"
# Hides HDD options if PS2 is SCPH-75k or later
IF GTE "$BM.BIOS_MAJOR_VER$$BM.BIOS_MINOR_VER$" "220"
    SET "SPEEDCHIP" "0"
ENDIF

GOTO "$ARG1$"
EXIT -1

:MENU_CONFIG
    CLEARWIDGETS
    SETTITLE "$BM.TXT_CONFIGURATION$"
    IF EQU "$BM.CNF_PIN_NUM0$$BM.CNF_PIN_NUM1$" "00"
        ADDWIDGET "CALL" "$BM.TXT_VID_WIZ$" "$BM.TXT_VID_WIZ_1$" "$PWD$/TVTEST.PBT" "TEST_VIDEO"
        ADDWIDGET "LABEL" ""
        ADDWIDGET "LABEL" "$BM.TXT_BM_CONFIG$"
        ADDWIDGET "CALL" "  $BM.TXT_INTERF_CONFIG$" "$BM.TXT_HINT_BM_CONFIG$" "$ARG0$" "MENU_BM_CONFIG"
        ADDWIDGET "CALL" "  $BM.TXT_AUTOLOAD_CONFIG$" "$BM.TXT_HINT_AUTOLOAD_CONFIG$" "$ARG0$" "MENU_AL_CONFIG"
        ADDWIDGET "CALL" "  $BM.TXT_THEME_MANAGER$" "$BM.TXT_HINT_THEME_MANAGER$" "$ARG0$" "MENU_THEME_MANAGER"
        IF EXISTS "$BM.BM_PATH$/DEVS/NET"
            ADDWIDGET "CALL" "  $BM.TXT_NET_CONFIG$" "$BM.TXT_HINT_NET_CONFIG$" "$ARG0$" "MENU_NET_CONFIG"
        ELSE
            ADDWIDGET "CALL" "  $BM.TXT_NET_DESC$ $BM.TXT_MISSING$!" "$BM.TXT_HINT_NET_CONFIG$" "$ARG0$" "MENU_NET_CONFIG"
        ENDIF
        IF EXISTS "$BM.BM_PATH$/DEVS/PS2HDD"
            ADDWIDGET "CALL" "  $BM.TXT_HDD_CONFIG$" "$BM.TXT_HINT_HDD_CONFIG$" "$ARG0$" "MENU_HDD_CONFIG"
        ELSE 
            ADDWIDGET "LABEL" "  $BM.TXT_HDD_DESC_1$ $BM.TXT_MISSING$!"
        ENDIF
    ENDIF
        ADDWIDGET "CALL" "  $BM.TXT_CNF_LOCKOUT$" "$BM.TXT_CNF_LOCKOUT_HINT$" "$ARG0$" "MENU_LOCKOUT"
    IF EQU "$BM.CNF_PIN_NUM0$$BM.CNF_PIN_NUM1$" "00"
        ADDWIDGET "CALL" "  $BM.TXT_MISC_CONFIG$" "$BM.TXT_HINT_MISC_CONFIG$" "$ARG0$" "MENU_MISC_CONFIG"
        ADDWIDGET "LABEL" ""
        ADDWIDGET "LABEL" "$BM.TXT_SOFTWARE_CONFIG$"
        ADDWIDGET "CALL" "  $BM.TXT_PS1_CONFIG$" "$BM.TXT_HINT_PS1_CONFIG$" "$ARG0$" "MENU_PS1_CONFIG"
        ADDWIDGET "CALL" "  $BM.TXT_PS2_CONFIG$" "$BM.TXT_HINT_PS2_CONFIG$" "$ARG0$" "MENU_PS2_CONFIG"
        ADDWIDGET "CALL" "  $BM.TXT_DVD_CONFIG$" "$BM.TXT_HINT_DVD_CONFIG$" "$ARG0$" "MENU_DVD_CONFIG"
    ENDIF
    ADDWIDGET "LABEL" ""
    ADDWIDGET "LABEL" "$BM.TXT_TUTORIAL$"
    ADDWIDGET "CALL" "  $BM.TXT_MENU_BMRTFLDR$" "$BM.TXT_MENU_BMRTFLDR_HINT$" "$ARG0$" "MENU_BMRTFLDR_TUTORIAL"
    ADDWIDGET "CALL" "  $BM.TXT_MENU_HK$" "$BM.TXT_MENU_HK_HINT$" "$ARG0$" "MENU_HOTKEY_TUTORIAL"
    ADDWIDGET "LABEL" "  PS2MODCHIPTUTORIALS.COM"
    ADDWIDGET "CALL" "" "            Hidden menu ;-)" "$ARG0$" "MENU_THANKS"
    ADDWIDGET "CALL" "$BM.TXT_BACK$" "$BM.TXT_HINT_BACK$" "$ARG0$" "PSEUDO"
    RETURN 0

:MENU_BM_CONFIG
    CLEARWIDGETS
    SETTITLE "$BM.TXT_BM_CONFIG$"
    ADDWIDGET "CALL" "$BM.TXT_LANGUAGE$" "$BM.TXT_HINT_BM_LANG$" "$ARG0$" "MENU_CHOOSE_LANG"
    LOADEXEC "PBAT" "$BM.CNF_LANG$" "LABEL_NAME" "  $BM.TXT_CURRENT$: "
    ADDWIDGET "CHOICE" "$BM.TXT_BM_VIDEO_MODE$" "$BM.TXT_HINT_BM_VIDEO_MODE$" "BM.CNF_VMODE" "NTSC" "PAL" "VGA" "480P"
    ADDWIDGET "AXIS" "$BM.TXT_BM_SCREEN_POS$" "$BM.TXT_HINT_BM_SCREEN_POS$" "BM.CNF_SCREEN_X" "BM.CNF_SCREEN_Y" "0" "1920" "1" "0" "1920" "2"
    ADDWIDGET "CHOICE" "$BM.TXT_HINT_SCROLLBAR$" "$BM.TXT_HINT_HINT_SCROLLBAR$" "BM.CNF_SCROLLER_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"
    IF EQU "$BM.CNF_SCROLLER_EN$" "1"
        ADDWIDGET "INT" "$BM.TXT_SCROLLBAR_SPEED$" "$BM.TXT_HINT_SCROLLBAR_SPEED$" "BM.CNF_SCROLLER_SPEED" "1" "10" "1"
    ENDIF
    ADDWIDGET "CHOICE" "$BM.TXT_APP_DIR$" "$BM.TXT_HINT_APP_DIR$" "BM.CNF_APPDIR" "$BM.TXT_OFF$" "$BM.TXT_ON$"
    ADDWIDGET "CHOICE" "$BM.TXT_CONFIRM_REM_INST$" "$BM.TXT_CONFIRM_REM_INST_HINT$" "BM.CNF_REMOVE_INSTALL_CONFIRM" "$BM.TXT_OFF$" "$BM.TXT_MENU_REMOVE$" "$BM.TXT_MENU_INSTALL$" "$BM.TXT_BOTH$" 
    ADDWIDGET "LABEL" ""
    ADDWIDGET "CALL" "$BM.TXT_INSTALL_LANG$" "$BM.TXT_HINT_INSTALL_LANG$" "$ARG0$" "MENU_INST_LANG_DEV"
    ADDWIDGET "CALL" "$BM.TXT_REMOVE_LANG$" "$BM.TXT_HINT_REMOVE_LANG$" "$ARG0$" "MENU_REMOVE_LANG"
    RETURN 0

:MENU_NET_CONFIG
    CLEARWIDGETS
    SETTITLE "$BM.TXT_NET_CONFIG$"

    IF EXISTS "$BM.BM_PATH$/DEVS/NET"
        IF NEQ "$BM.NET_STARTED$" "1"
                ADDWIDGET "CALL" "$BM.TXT_START_NET$" "$BM.TXT_HINT_START_NET$" "$ARG0$" "START_NET"
                ADDWIDGET "LABEL" ""
        ENDIF

        IF EXISTS "$BM.BM_PATH$/DEVS/HOST"
            IF NEQ "$BM.HOST_STARTED$" "1"
                ADDWIDGET "CALL" "$BM.TXT_START_HOST$" "$BM.TXT_HINT_START_HOST$" "$ARG0$" "START_HOST"
                ADDWIDGET "LABEL" ""
            ENDIF
        ELSE
            ADDWIDGET "LABEL" "$BM.TXT_HOST_DESC_1$ $BM.TXT_MISSING$!"
            ADDWIDGET "LABEL" ""
        ENDIF

    # FTP currently disabled by default due to instability in PS2FTPD.IRX or associated drivers...
        IF EXISTS "$BM.BM_PATH$/DEVS/FTP"
            IF NEQ "$BM.FTPD_STARTED$" "1"
                ADDWIDGET "CALL" "$BM.TXT_START_FTP$" "$BM.TXT_HINT_START_FTP$" "$ARG0$" "START_FTPD"
                ADDWIDGET "LABEL" ""
            ENDIF
        ELSE
            ADDWIDGET "LABEL" "$BM.TXT_FTP_DESC$ $BM.TXT_MISSING$!"
            ADDWIDGET "LABEL" ""
        ENDIF
    ELSE
        ADDWIDGET "LABEL" "$BM.TXT_NET_DESC$ $BM.TXT_MISSING$!"
        ADDWIDGET "LABEL" ""
    ENDIF

    IF EXISTS "$BM.BM_PATH$/DEVS/NET"
        ADDWIDGET "CHOICE" "$BM.TXT_AUTOSTART_NET$" "$BM.TXT_HINT_AUTOSTART_NET$" "BM.CNF_AUTO_NET_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"
        IF EXISTS "$BM.BM_PATH$/DEVS/HOST"
            ADDWIDGET "CHOICE" "$BM.TXT_AUTOSTART_HOST$" "$BM.TXT_HINT_AUTOSTART_HOST$" "BM.CNF_AUTO_HOST_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"
        ENDIF
        IF EXISTS "$BM.BM_PATH$/DEVS/FTP"
            ADDWIDGET "CHOICE" "$BM.TXT_AUTOSTART_FTP$" "$BM.TXT_HINT_AUTOSTART_FTP$" "BM.CNF_AUTO_FTPD_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"
        ENDIF
    ENDIF

    ADDWIDGET "IP" "$BM.TXT_PS2_IP$" "$BM.TXT_HINT_PS2_IP$" "BM.CNF_NET_IP0" "BM.CNF_NET_IP1" "BM.CNF_NET_IP2" "BM.CNF_NET_IP3"
    ADDWIDGET "IP" "$BM.TXT_SUBNET_MASK$" "$BM.TXT_HINT_SUBNET_MASK$" "BM.CNF_NET_SM0" "BM.CNF_NET_SM1" "BM.CNF_NET_SM2" "BM.CNF_NET_SM3"
    ADDWIDGET "IP" "$BM.TXT_DEF_GATEWAY$" "$BM.TXT_HINT_DEF_GATEWAY$" "BM.CNF_NET_DG0" "BM.CNF_NET_DG1" "BM.CNF_NET_DG2" "BM.CNF_NET_DG3"
    ADDWIDGET "IP" "$BM.TXT_PRIMARY_DNS$" "$BM.TXT_HINT_PRIMARY_DNS$" "BM.CNF_NET_PDNS0" "BM.CNF_NET_PDNS1" "BM.CNF_NET_PDNS2" "BM.CNF_NET_PDNS3"
    ADDWIDGET "IP" "$BM.TXT_SECONDARY_DNS$" "$BM.TXT_HINT_SECONDARY_DNS$" "BM.CNF_NET_SDNS0" "BM.CNF_NET_SDNS1" "BM.CNF_NET_SDNS2" "BM.CNF_NET_SDNS3"

    RETURN 0

:MENU_HDD_CONFIG
    CLEARWIDGETS
    SETTITLE "$BM.TXT_HDD_CONFIG$"
    IF EXISTS "$BM.BM_PATH$/DEVS/PS2HDD"
        IF NEQ "$BM.HDD_STARTED$" "1"
            ADDWIDGET "CALL" "$BM.TXT_START_HDD$" "$BM.TXT_HINT_START_HDD$" "$ARG0$" "START_HDD"
            ADDWIDGET "LABEL" ""
        ENDIF
        ADDWIDGET "CHOICE" "$BM.TXT_AUTOSTART_HDD$" "$BM.TXT_HINT_AUTOSTART_HDD$" "BM.CNF_AUTO_HDD_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"
    ELSE
        ADDWIDGET "LABEL" "$BM.TXT_HDD_DESC_1$ $BM.TXT_MISSING$!"
    ENDIF
    RETURN 0

:MENU_LOCKOUT
    CLEARWIDGETS
    SETTITLE "$BM.TXT_CNF_LOCKOUT$"
    IF EQU "$BM.CNF_PIN_NUM0$$BM.CNF_PIN_NUM1$" "00"
        ADDWIDGET "LABEL" "$BM.TXT_CNF_LOCKOUT_LABEL1$"
        ADDWIDGET "LABEL" "$BM.TXT_CNF_LOCKOUT_LABEL2$"
        ADDWIDGET "LABEL" ""
        ADDWIDGET "AXIS" "$BM.TXT_PIN_SETUP$" "$BM.TXT_PIN_SETUP_HINT$" "TMP_PIN0" "TMP_PIN1" "0" "99" "1" "0" "99" "1"
        ADDWIDGET "AXIS" "$BM.TXT_PIN_CONF$" "$BM.TXT_PIN_CONF_HINT$" "CONF_TMP_PIN0" "CONF_TMP_PIN1" "0" "99" "1" "0" "99" "1"
            IF EQU "$TMP_PIN0$$TMP_PIN1$" "$CONF_TMP_PIN0$$CONF_TMP_PIN1$"
                SET "U8" "BM.CNF_PIN_NUM0" "$TMP_PIN0$"
                SET "U8" "BM.CNF_PIN_NUM1" "$TMP_PIN1$"
#                MESSAGE "$BM.TXT_PIN_SET$"
                SET "BM.CONFIG_CHANGED" "1"
            ELSEIF NEQ "$CONF_TMP_PIN0$$CONF_TMP_PIN1$" "00"
                UNSET "CONF_PIN0"
                UNSET "CONF_PIN1"
                UNSET "TMP_PIN0"
                UNSET "TMP_PIN1"
                UNSET "CONF_TMP_PIN0"
                UNSET "CONF_TMP_PIN1"
                CLEARWIDGETS
                SKIPBACK
            ENDIF
    ELSE
        ADDWIDGET "LABEL" "$BM.TXT_CNF_LOCKOUT_LABEL3$"
        ADDWIDGET "LABEL" "$BM.TXT_CNF_LOCKOUT_LABEL4$"
        ADDWIDGET "LABEL" ""
        ADDWIDGET "AXIS" "$BM.TXT_PIN_UNLOCK$" "$BM.TXT_PIN_UNLOCK_HINT$" "CONF_PIN0" "CONF_PIN1" "0" "99" "1" "0" "99" "1"
            IF EQU "$BM.CNF_PIN_NUM0$$BM.CNF_PIN_NUM1$" "$CONF_PIN0$$CONF_PIN1$"
                SET "U8" "BM.CNF_PIN_NUM0" "0"
                SET "U8" "BM.CNF_PIN_NUM1" "0"
#                MESSAGE "$BM.TXT_UNLOCKED$"
                SET "BM.CONFIG_CHANGED" "1"
            ELSEIF EQU "$BM.CNF_PIN_NUM0$$BM.CNF_PIN_NUM1$" "$CONF_PIN0$$CONF_PIN1$"
                UNSET "CONF_PIN0"
                UNSET "CONF_PIN1"
                UNSET "TMP_PIN0"
                UNSET "TMP_PIN1"
                UNSET "CONF_TMP_PIN0"
                UNSET "CONF_TMP_PIN1"
                CLEARWIDGETS
                SKIPBACK
 #           ELSEIF NEQ "$BM.CNF_PIN_NUM0$$BM.CNF_PIN_NUM1$" "$CONF_PIN0$$CONF_PIN1$"
 #               MESSAGE "$BM.TXT_INCORRECT_PIN$"
            ENDIF
    ENDIF
    RETURN 0

:MENU_MISC_CONFIG
    CLEARWIDGETS
    SETTITLE "$BM.TXT_MISC_CONFIG$"
    ADDWIDGET "CHOICE" "$BM.TXT_GS_HOOK$" "$BM.TXT_HINT_GS_HOOK$" "BM.CNF_GS_HOOK_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"
    ADDWIDGET "CHOICE" "$BM.TXT_IGR$" "$BM.TXT_HINT_IGR$" "BM.CNF_IGR_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"

    RETURN 0

:START_HDD
    CLEARWIDGETS
    LOADEXEC "PBAT" "$BM.SCRIPTS$/LOADHDD.PBT"
    RETURN 0

:START_NET
    CLEARWIDGETS
    LOADEXEC "PBAT" "$BM.SCRIPTS$/LOADNET.PBT"
    RETURN 0

:START_HOST
    CLEARWIDGETS
    LOADEXEC "PBAT" "$BM.SCRIPTS$/LOADHOST.PBT"
    RETURN 0

:START_FTPD
    CLEARWIDGETS
    LOADEXEC "PBAT" "$BM.SCRIPTS$/LOADFTPD.PBT"
    RETURN 0

:MENU_AL_CONFIG
    CLEARWIDGETS
    SETTITLE "$BM.TXT_AUTOLOAD_CONFIG$"
    SET "ARG1" "^"$ARG0$^" ^"MENU_CHOOSE_DEV^""
    SET "ARG2" "^"APP^"  ^"SET_LOAD_APP^""
    ADDWIDGET "CHOICE" "$BM.TXT_AUTOLOAD$" "$BM.TXT_HINT_AUTOLOAD$" "BM.CNF_AUTOLOAD_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"

    IF EQU "$BM.CNF_AUTOLOAD_EN$" "1"

        ADDWIDGET "LABEL" "  $BM.TXT_AUTOLOAD_PS1$"
        ADDWIDGET "CHOICE" "    $BM.TXT_ACTION$" "$BM.TXT_HINT_AL_ACT_PS1$" "BM.CNF_AL_PS1_CMD" "$BM.TXT_OFF$" "$BM.TXT_APP$" "$BM.TXT_BOOT$"
        IF NOT EQU "$BM.CNF_AL_PS1_CMD$" "0"
            IF EQU "$BM.CNF_AL_PS1_CMD$" "1"
                ADDWIDGET "CALL" "    $BM.TXT_SET_AL_APP$" "$BM.TXT_HINT_SET_AL_APP$" $ARG1$ "AL_PS1" $ARG2$
                LOADEXEC "PBAT" "$BM.CNF_AL_PS1_PBT$" "LABEL_NAME" "      $BM.TXT_CURRENT$: "
            ELSE
                SET "BM.CNF_AL_PS1_PBT" "$BM.SCRIPTS$/BOOTMENU.PBT"
                SET "BM.CNF_AL_PS1_PBT_ARGS" "^"BOOT^" ^"PS1^""
            ENDIF

            ADDWIDGET "INT" "    $BM.TXT_AL_DELAY$" "$BM.TXT_HINT_AL_DELAY$" "BM.CNF_AL_PS1_DELAY" "0" "300" "1"
        ELSE
            UNSET "BM.CNF_AL_PS1_PBT"
            UNSET "BM.CNF_AL_PS1_PBT_ARGS"
        ENDIF

        ADDWIDGET "LABEL" "  $BM.TXT_AUTOLOAD_PS2$"
        ADDWIDGET "CHOICE" "    $BM.TXT_ACTION$" "$BM.TXT_HINT_AL_ACT_PS2$" "BM.CNF_AL_PS2_CMD" "$BM.TXT_OFF$" "$BM.TXT_APP$" "$BM.TXT_BOOT$"
        IF NOT EQU "$BM.CNF_AL_PS2_CMD$" "0"
            IF EQU "$BM.CNF_AL_PS2_CMD$" "1"
                ADDWIDGET "CALL" "    $BM.TXT_SET_AL_APP$" "$BM.TXT_HINT_SET_AL_APP$" $ARG1$ "AL_PS2" $ARG2$
                LOADEXEC "PBAT" "$BM.CNF_AL_PS2_PBT$" "LABEL_NAME" "      $BM.TXT_CURRENT$: "
            ELSE
                SET "BM.CNF_AL_PS2_PBT" "$BM.SCRIPTS$/BOOTMENU.PBT"
                SET "BM.CNF_AL_PS2_PBT_ARGS" "^"BOOT^" ^"PS2^""
            ENDIF

            ADDWIDGET "INT" "    $BM.TXT_AL_DELAY$" "$BM.TXT_HINT_AL_DELAY$" "BM.CNF_AL_PS2_DELAY" "0" "300" "1"
        ELSE
            UNSET "BM.CNF_AL_PS2_PBT"
            UNSET "BM.CNF_AL_PS2_PBT_ARGS"
        ENDIF

        ADDWIDGET "LABEL" "  $BM.TXT_AUTOLOAD_DVDV$"
        ADDWIDGET "CHOICE" "    $BM.TXT_ACTION$" "$BM.TXT_HINT_AL_ACT_DVDV$" "BM.CNF_AL_DVDV_CMD" "$BM.TXT_OFF$" "$BM.TXT_APP$" "$BM.TXT_BOOT$"
        IF NOT EQU "$BM.CNF_AL_DVDV_CMD$" "0"
            IF EQU "$BM.CNF_AL_DVDV_CMD$" "1"
                ADDWIDGET "CALL" "    $BM.TXT_SET_AL_APP$" "$BM.TXT_HINT_SET_AL_APP$" $ARG1$ "AL_DVDV" $ARG2$
                LOADEXEC "PBAT" "$BM.CNF_AL_DVDV_PBT$" "LABEL_NAME" "      $BM.TXT_CURRENT$: "
            ELSE
                SET "BM.CNF_AL_DVDV_PBT" "$BM.SCRIPTS$/BOOTMENU.PBT"
                SET "BM.CNF_AL_DVDV_PBT_ARGS" "^"BOOT^" ^"DVDV^""
            ENDIF

            ADDWIDGET "INT" "    $BM.TXT_AL_DELAY$" "$BM.TXT_HINT_AL_DELAY$" "BM.CNF_AL_DVDV_DELAY" "0" "300" "1"
        ELSE
            UNSET "BM.CNF_AL_DVDV_PBT"
            UNSET "BM.CNF_AL_DVDV_PBT_ARGS"
        ENDIF

        ADDWIDGET "LABEL" "  $BM.TXT_AUTOLOAD_CDDA$"
        ADDWIDGET "CHOICE" "    $BM.TXT_ACTION$" "$BM.TXT_HINT_AL_ACT_CDDA$" "BM.CNF_AL_CDDA_CMD" "$BM.TXT_OFF$" "$BM.TXT_APP$" "$BM.TXT_BOOT$"
        IF NOT EQU "$BM.CNF_AL_CDDA_CMD$" "0"
            IF EQU "$BM.CNF_AL_CDDA_CMD$" "1"
                ADDWIDGET "CALL" "    $BM.TXT_SET_AL_APP$" "$BM.TXT_HINT_SET_AL_APP$" $ARG1$ "AL_CDDA" $ARG2$
                LOADEXEC "PBAT" "$BM.CNF_AL_CDDA_PBT$" "LABEL_NAME" "      $BM.TXT_CURRENT$: "
            ELSE
                SET "BM.CNF_AL_CDDA_PBT" "$BM.SCRIPTS$/BOOTMENU.PBT"
                SET "BM.CNF_AL_CDDA_PBT_ARGS" "^"BOOT^" ^"CDDA^""
            ENDIF

            ADDWIDGET "INT" "    $BM.TXT_AL_DELAY$" "$BM.TXT_HINT_AL_DELAY$" "BM.CNF_AL_CDDA_DELAY" "0" "300" "1"
        ELSE
            UNSET "BM.CNF_AL_CDDA_PBT"
            UNSET "BM.CNF_AL_CDDA_PBT_ARGS"
        ENDIF

        ADDWIDGET "LABEL" "  $BM.TXT_AUTOLOAD_OTHER$"
        ADDWIDGET "CHOICE" "    $BM.TXT_ACTION$" "$BM.TXT_HINT_AL_ACT_OTHER$" "BM.CNF_AL_OTHER_CMD" "$BM.TXT_OFF$" "$BM.TXT_APP$" "$BM.TXT_BOOT$"
        IF EQU "$BM.CNF_AL_OTHER_CMD$" "1"
            ADDWIDGET "CALL" "    $BM.TXT_SET_AL_APP$" "$BM.TXT_HINT_SET_AL_APP$" $ARG1$ "AL_OTHER" $ARG2$
            LOADEXEC "PBAT" "$BM.CNF_AL_OTHER_PBT$" "LABEL_NAME" "      $BM.TXT_CURRENT$: "
            ADDWIDGET "INT"  "    $BM.TXT_AL_DELAY$" "$BM.TXT_HINT_AL_DELAY$" "BM.CNF_AL_OTHER_DELAY" "0" "300" "1"
        ELSE
            UNSET "BM.CNF_AL_OTHER_PBT"
            UNSET "BM.CNF_AL_OTHER_PBT_ARGS"
        ENDIF

        ADDWIDGET "LABEL"  "  $BM.TXT_AUTOLOAD_NODISC$"
        ADDWIDGET "CHOICE" "    $BM.TXT_ACTION$" "$BM.TXT_HINT_AL_ACT_NODISC$" "BM.CNF_AL_NODISC_CMD" "$BM.TXT_OFF$" "$BM.TXT_APP$"
        IF EQU "$BM.CNF_AL_NODISC_CMD$" "1"
            ADDWIDGET "CALL" "    $BM.TXT_SET_AL_APP$" "$BM.TXT_HINT_SET_AL_APP$" $ARG1$ "AL_NODISC" $ARG2$
            LOADEXEC "PBAT" "$BM.CNF_AL_NODISC_PBT$" "LABEL_NAME" "      $BM.TXT_CURRENT$: "
            ADDWIDGET "INT"  "    $BM.TXT_AL_DELAY$" "$BM.TXT_HINT_AL_DELAY$" "BM.CNF_AL_NODISC_DELAY" "0" "300" "1"
        ELSE
            UNSET "BM.CNF_AL_NODISC_PBT"
            UNSET "BM.CNF_AL_NODISC_PBT_ARGS"
        ENDIF
    ENDIF
    RETURN 0

:MENU_PS1_CONFIG
    CLEARWIDGETS
    SETTITLE "$BM.TXT_PS1_CONFIG$"
    SET "BM.DBG" "1"
    ADDWIDGET "CHOICE" "$BM.TXT_DISC_SPEED$" "$BM.TXT_HINT_DISC_SPEED$" "BM.CNF_PS1_DISC_SPEED" "$BM.TXT_STANDARD$" "$BM.TXT_FAST$"
    ADDWIDGET "CHOICE" "$BM.TXT_TEX_MAPPING$" "$BM.TXT_HINT_DISC_SPEED$" "BM.CNF_PS1_TEX_MAP" "$BM.TXT_STANDARD$" "$BM.TXT_SMOOTH$"

    ADDWIDGET "CHOICE" "$BM.TXT_NTSC_SW_VID_FIXES$" "$BM.TXT_HINT_NTSC_SW_VID_FIXES$" "BM.CNF_PS1_NTSC_VFIX_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"
    IF EQU "$BM.CNF_PS1_NTSC_VFIX_EN$" "1"
        ADDWIDGET "CHOICE" "  $BM.TXT_FORCE_VSYNC$" "$BM.TXT_HINT_FORCE_VSYNC$" "BM.CNF_PS1_NTSC_VSYNC" "$BM.TXT_OFF$" "$BM.TXT_AUTO$" "50Hz" "60Hz"
        ADDWIDGET "CHOICE" "  $BM.TXT_FORCE_COLOR$" "$BM.TXT_HINT_FORCE_COLOR$" "BM.CNF_PS1_NTSC_COLOR" "$BM.TXT_OFF$" "$BM.TXT_AUTO$" "NTSC" "PAL"
        ADDWIDGET "AXIS" "  $BM.TXT_SCREEN_MOD$" "$BM.TXT_HINT_SCREEN_MOD$" "BM.CNF_PS1_NTSC_X_MOD" "BM.CNF_PS1_NTSC_Y_MOD" "-100" "100" "1" "-100" "100" "2"
    ENDIF

    ADDWIDGET "CHOICE" "$BM.TXT_PAL_SW_VID_FIXES$" "$BM.TXT_HINT_PAL_SW_VID_FIXES$" "BM.CNF_PS1_PAL_VFIX_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"
    IF EQU "$BM.CNF_PS1_PAL_VFIX_EN$" "1"
        ADDWIDGET "CHOICE" "  $BM.TXT_FORCE_VSYNC$" "$BM.TXT_HINT_FORCE_VSYNC$" "BM.CNF_PS1_PAL_VSYNC" "$BM.TXT_OFF$" "$BM.TXT_AUTO$" "50Hz" "60Hz"
        ADDWIDGET "CHOICE" "  $BM.TXT_FORCE_COLOR$" "$BM.TXT_HINT_FORCE_COLOR$" "BM.CNF_PS1_PAL_COLOR" "$BM.TXT_OFF$" "$BM.TXT_AUTO$" "NTSC" "PAL"
        ADDWIDGET "AXIS" "  $BM.TXT_SCREEN_MOD$" "$BM.TXT_HINT_SCREEN_MOD$" "BM.CNF_PS1_PAL_X_MOD" "BM.CNF_PS1_PAL_Y_MOD" "-100" "100" "1" "-100" "100" "2"
    ENDIF
    RETURN 0

:MENU_PS2_CONFIG
    CLEARWIDGETS
    SETTITLE "$BM.TXT_PS2_CONFIG$"
    IF EQU "$BM.BIOS_MAJOR_VER$" "1"
        ADDWIDGET "CHOICE" "$BM.TXT_MEGAMEM_PATCHER$" "$BM.TXT_HINT_MEGAMEM_PATCHER$" "BM.CNF_MM16_PATCH_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"
    ENDIF
    IF NEQ "$SPEEDCHIP$" "0"
        ADDWIDGET "CHOICE" "$BM.TXT_ATAD_PATCHER$" "$BM.TXT_HINT_ATAD_PATCHER$" "BM.CNF_ATAD_PATCH_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"
    ENDIF
    ADDWIDGET "CHOICE" "$BM.TXT_DVDDL_PATCHER$" "$BM.TXT_HINT_DVDDL_PATCHER$" "BM.CNF_DVDDL_PATCH_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"

    ADDWIDGET "LABEL" ""
    ADDWIDGET "CHOICE" "$BM.TXT_NTSC_SW_VID_FIXES$" "$BM.TXT_HINT_NTSC_SW_VID_FIXES$" "BM.CNF_PS2_NTSC_VFIX_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"
    IF EQU "$BM.CNF_PS2_NTSC_VFIX_EN$" "1"
        ADDWIDGET "CHOICE" "  $BM.TXT_FORCE_VSYNC$" "$BM.TXT_HINT_FORCE_VSYNC$" "BM.CNF_PS2_NTSC_VSYNC" "$BM.TXT_OFF$" "$BM.TXT_AUTO$" "50Hz" "60Hz"
        ADDWIDGET "CHOICE" "  $BM.TXT_FORCE_COLOR$" "$BM.TXT_HINT_FORCE_COLOR$" "BM.CNF_PS2_NTSC_COLOR" "$BM.TXT_OFF$" "$BM.TXT_AUTO$" "NTSC" "PAL"
        ADDWIDGET "CHOICE" "  $BM.TXT_SCREEN_MODS$" "$BM.TXT_HINT_SCREEN_MODS$" "BM.CNF_PS2_NTSC_MODS_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"
        IF EQU "$BM.CNF_PS2_NTSC_MODS_EN$" "1"
            ADDWIDGET "AXIS" "  $BM.TXT_SCREEN_MOD$" "$BM.TXT_HINT_SCREEN_MOD$" "BM.CNF_PS2_NTSC_X_MOD" "BM.CNF_PS2_NTSC_Y_MOD" "-100" "100" "1" "-100" "100" "2"
        ENDIF
    ENDIF

    ADDWIDGET "LABEL" ""
    ADDWIDGET "CHOICE" "$BM.TXT_PAL_SW_VID_FIXES$" "$BM.TXT_HINT_PAL_SW_VID_FIXES$" "BM.CNF_PS2_PAL_VFIX_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"
    IF EQU "$BM.CNF_PS2_PAL_VFIX_EN$" "1"
        ADDWIDGET "CHOICE" "  $BM.TXT_FORCE_VSYNC$" "$BM.TXT_HINT_FORCE_VSYNC$" "BM.CNF_PS2_PAL_VSYNC" "$BM.TXT_OFF$" "$BM.TXT_AUTO$" "50Hz" "60Hz"
        ADDWIDGET "CHOICE" "  $BM.TXT_FORCE_COLOR$" "$BM.TXT_HINT_FORCE_COLOR$" "BM.CNF_PS2_PAL_COLOR" "$BM.TXT_OFF$" "$BM.TXT_AUTO$" "NTSC" "PAL"
        ADDWIDGET "CHOICE" "  $BM.TXT_SCREEN_MODS$" "$BM.TXT_HINT_SCREEN_MODS$" "BM.CNF_PS2_PAL_MODS_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"
        IF EQU "$BM.CNF_PS2_PAL_MODS_EN$" "1"
            ADDWIDGET "AXIS" "  $BM.TXT_SCREEN_MOD$" "$BM.TXT_HINT_SCREEN_MOD$" "BM.CNF_PS2_PAL_X_MOD" "BM.CNF_PS2_PAL_Y_MOD" "-100" "100" "1" "-100" "100" "2"
        ENDIF
    ENDIF
    RETURN 0

:MENU_DVD_CONFIG
    CLEARWIDGETS
    SETTITLE "$BM.TXT_DVD_CONFIG$"
    ADDWIDGET "CHOICE" "$BM.TXT_DVD_VIDEO_FIXES$" "$BM.TXT_HINT_DVD_VIDEO_FIXES$" "BM.CNF_DVD_FIXES_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"
    IF EQU "$BM.CNF_DVD_FIXES_EN$" "1"
        ADDWIDGET "CHOICE" "  $BM.TXT_REGION_FREE$" "$BM.TXT_HINT_REGION_FREE$" "BM.CNF_DVD_REGION_FREE_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"
        IF EQU "$BM.CNF_DVD_REGION_FREE_EN$" "1"
            ADDWIDGET "INT"    "  $BM.TXT_DVD_PLAYER_REGION$" "$BM.TXT_HINT_DVD_PLAYER_REGION$" "BM.CNF_DVD_RCE_REGION" "0" "9" "1"
        ENDIF
        ADDWIDGET "CHOICE" "  $BM.TXT_RGB_FIX$" "$BM.TXT_HINT_RGB_FIX$" "BM.CNF_DVD_RGB_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"
        ADDWIDGET "CHOICE" "  $BM.TXT_VIDEO_STAB$" "$BM.TXT_HINT_VIDEO_STAB$" "BM.CNF_DVD_VSTAB_EN" "$BM.TXT_OFF$" "$BM.TXT_ON$"
        ADDWIDGET "CHOICE" "  $BM.TXT_FORCE_COLOR$" "$BM.TXT_HINT_FORCE_COLOR_DVD$" "BM.CNF_DVD_COLOR" "$BM.TXT_OFF$" "NTSC" "PAL"
    ENDIF

    RETURN 0

:MENU_HOTKEY_TUTORIAL
    CLEARWIDGETS
    SETTITLE "$BM.TXT_TITLE_HK$"
    ADDWIDGET "LABEL" "$BM.TXT_HK_TUTORIAL1$"
    ADDWIDGET "LABEL" "$BM.TXT_HK_TUTORIAL2$"
    ADDWIDGET "LABEL" "$BM.TXT_HK_TUTORIAL3$"
    ADDWIDGET "LABEL" "$BM.TXT_HK_TUTORIAL4$"
    ADDWIDGET "LABEL" "$BM.TXT_HK_TUTORIAL5$"
    ADDWIDGET "LABEL" "$BM.TXT_HK_TUTORIAL6$"
    ADDWIDGET "LABEL" "$BM.TXT_HK_TUTORIAL7$"
    ADDWIDGET "LABEL" ""
    ADDWIDGET "CALL" "$BM.TXT_BACK$" "$BM.TXT_HINT_BACK$" "$ARG0$" "PSEUDO"
    RETURN 0

:MENU_BMRTFLDR_TUTORIAL
    CLEARWIDGETS
    SETTITLE "$BM.TXT_TITLE_BMRTFLDR$"
    ADDWIDGET "LABEL" "$BM.TXT_BMRTFLDR_TUTORIAL1$"
    ADDWIDGET "LABEL" "$BM.TXT_BMRTFLDR_TUTORIAL2$"
    ADDWIDGET "LABEL" "$BM.TXT_BMRTFLDR_TUTORIAL3$"
    ADDWIDGET "LABEL" "$BM.TXT_BMRTFLDR_TUTORIAL4$"
    ADDWIDGET "LABEL" "$BM.TXT_BMRTFLDR_TUTORIAL5$"
    ADDWIDGET "LABEL" "$BM.TXT_BMRTFLDR_TUTORIAL6$"
    ADDWIDGET "LABEL" "$BM.TXT_BMRTFLDR_TUTORIAL7$"
    ADDWIDGET "LABEL" "$BM.TXT_BMRTFLDR_TUTORIAL8$"
    ADDWIDGET "LABEL" ""
    ADDWIDGET "CALL" "$BM.TXT_BACK$" "$BM.TXT_HINT_BACK$" "$ARG0$" "PSEUDO"
    RETURN 0

:MENU_THANKS
    CLEARWIDGETS
    SETTITLE "You found the Easter Egg!"
    ADDWIDGET "LABEL" "Thanks to:"
    ADDWIDGET "LABEL" "  Crystal Chip Team"
    ADDWIDGET "LABEL" "  ASMBLUR"
    ADDWIDGET "LABEL" "  AtherisMods"
    ADDWIDGET "LABEL" "  SylverRez"
    ADDWIDGET "LABEL" "  Jones23 - 2007 Scripting"
    ADDWIDGET "LABEL" "  Wanderer - French Translation"
    ADDWIDGET "LABEL" "  R3Z3N - 2025 updates"
    ADDWIDGET "LABEL" "  Many who may never be known..."
    ADDWIDGET "CALL" "" "" "" ""
    ADDWIDGET "LABEL" "No Thanks to:"
    ADDWIDGET "LABEL" "  TnA"
    ADDWIDGET "LABEL" "  r/PS2 and Mods"
    ADDWIDGET "LABEL" ""
    ADDWIDGET "CALL" "$BM.TXT_BACK$" "$BM.TXT_HINT_BACK$" "$ARG0$" "PSEUDO"
    RETURN 0

:MENU_THEME_MANAGER
    CLEARWIDGETS
    SKIPBACK
    CLEARWIDGETS
    SETTITLE "$BM.TXT_THEME_MANAGER$"
    LOADEXEC "PBAT" "$BM.CNF_THEME_SCRIPT$" "LABEL_NAME" "$BM.TXT_CURRENT$: "
    ADDWIDGET "CALL" "  $BM.TXT_CONFIGURATION$" "$BM.TXT_HINT_THEME_CONFIG$" "$ARG0$" "MENU_THEME_CONFIG"
    ADDWIDGET "LABEL" ""
    ADDWIDGET "LABEL" "$BM.TXT_MANAGE_THEME$"
    ADDWIDGET "CALL" "  $BM.TXT_MENU_CHANGE$" "$BM.TXT_HINT_THEME_CHANGE$" "$ARG0$" "MENU_CHOOSE_DEV" "CHANGE" "THM" "SET_THEME"
    ADDWIDGET "CALL" "  $BM.TXT_MENU_INSTALL$" "$BM.TXT_HINT_THEME_INSTALL$" "$ARG0$" "MENU_CHOOSE_DEV" "INSTALL" "THM" "MENU_CHOOSE_INST_DEV"
    # If BM.CNF_REMOVE_INSTALL_CONFIRM is 1 or 3 goto MENU_CONFIRM_YESNO. Otherwise if 0 or 2 do not confirm REMOVE.
    IF EQU "$BM.CNF_REMOVE_INSTALL_CONFIRM$" "1"
        ADDWIDGET "CALL" "  $BM.TXT_MENU_REMOVE$" "$BM.TXT_HINT_THEME_REMOVE$" "$ARG0$" "MENU_CHOOSE_DEV" "REMOVE" "THM" "MENU_CONFIRM_YESNO"
    ELSEIF EQU "$BM.CNF_REMOVE_INSTALL_CONFIRM$" "3"
        ADDWIDGET "CALL" "  $BM.TXT_MENU_REMOVE$" "$BM.TXT_HINT_THEME_REMOVE$" "$ARG0$" "MENU_CHOOSE_DEV" "REMOVE" "THM" "MENU_CONFIRM_YESNO"
    ELSEIF EQU "$BM.CNF_REMOVE_INSTALL_CONFIRM$" "0"
        ADDWIDGET "CALL" "  $BM.TXT_MENU_REMOVE$" "$BM.TXT_HINT_THEME_REMOVE$" "$ARG0$" "MENU_CHOOSE_DEV" "REMOVE" "THM" "ACTION"
    ELSEIF EQU "$BM.CNF_REMOVE_INSTALL_CONFIRM$" "2"
        ADDWIDGET "CALL" "  $BM.TXT_MENU_REMOVE$" "$BM.TXT_HINT_THEME_REMOVE$" "$ARG0$" "MENU_CHOOSE_DEV" "REMOVE" "THM" "ACTION"
    ENDIF
    RETURN 0

:MENU_THEME_CONFIG
    CLEARWIDGETS
    SETTITLE "$BM.TXT_THEME_CONFIG$"

    ADDWIDGET "LABEL" "$BM.TXT_THEME_MENU_IMG$"
    ADDWIDGET "AXIS" "  $BM.TXT_XY$" "$BM.TXT_HINT_THEME_MENU_IMG_XY$" "BM.THM_CNF_MENU_IMG_X" "BM.THM_CNF_MENU_IMG_Y" "0" "1000" "1" "0" "1000" "2"
    ADDWIDGET "AXIS" "  $BM.TXT_WH$" "$BM.TXT_HINT_THEME_MENU_IMG_WH$" "BM.THM_CNF_MENU_IMG_WIDTH" "BM.THM_CNF_MENU_IMG_HEIGHT" "0" "1000" "1" "0" "1000" "2"

    ADDWIDGET "LABEL" "$BM.TXT_THEME_MENU_TITLE$"
    ADDWIDGET "AXIS" "  $BM.TXT_XY$" "$BM.TXT_HINT_THEME_MENU_TITLE_XY$" "BM.THM_CNF_MENU_TITLE_X" "BM.THM_CNF_MENU_TITLE_Y" "0" "1000" "1" "0" "1000" "2"
    ADDWIDGET "AXIS" "  $BM.TXT_WH$" "$BM.TXT_HINT_THEME_MENU_TITLE_WH$" "BM.THM_CNF_MENU_TITLE_WIDTH" "BM.THM_CNF_MENU_TITLE_HEIGHT" "0" "1000" "1" "0" "1000" "2"

    ADDWIDGET "LABEL" "$BM.TXT_THEME_MENU_TEXT$"
    ADDWIDGET "AXIS" "  $BM.TXT_XY$" "$BM.TXT_HINT_THEME_MENU_XY$" "BM.THM_CNF_MENU_X" "BM.THM_CNF_MENU_Y" "0" "1000" "1" "0" "1000" "2"
    ADDWIDGET "AXIS" "  $BM.TXT_WH$" "$BM.TXT_HINT_THEME_MENU_WH$" "BM.THM_CNF_MENU_WIDTH" "BM.THM_CNF_MENU_HEIGHT" "0" "1000" "1" "0" "1000" "2"

    ADDWIDGET "LABEL" "$BM.TXT_THEME_SCROLLER$"
    ADDWIDGET "AXIS" "  $BM.TXT_XY$" "$BM.TXT_HINT_THEME_SCROLLER_XY$" "BM.THM_CNF_SCROLLER_X" "BM.THM_CNF_SCROLLER_Y" "0" "1000" "1" "0" "1000" "2"
    ADDWIDGET "AXIS" "  $BM.TXT_WH$" "$BM.TXT_HINT_THEME_SCROLLER_WH$" "BM.THM_CNF_SCROLLER_WIDTH" "BM.THM_CNF_SCROLLER_HEIGHT" "0" "1000" "1" "0" "1000" "2"

    ADDWIDGET "LABEL" "$BM.TXT_BM_VERSION$"
    ADDWIDGET "AXIS" "  $BM.TXT_XY$" "$BM.TXT_HINT_BM_VERSION$" "BM.THM_CNF_BM_VER_X" "BM.THM_CNF_BM_VER_Y" "0" "1000" "1" "0" "1000" "2"

    ADDWIDGET "LABEL" "$BM.TXT_THEME_DISC_IMG$"
    ADDWIDGET "AXIS" "  $BM.TXT_XY$" "$BM.TXT_HINT_DISC_IMG_XY$" "BM.THM_CNF_DISC_IMG_X" "BM.THM_CNF_DISC_IMG_Y" "0" "1000" "1" "0" "1000" "2"
    ADDWIDGET "AXIS" "  $BM.TXT_WH$" "$BM.TXT_HINT_DISC_IMG_WH$" "BM.THM_CNF_DISC_IMG_WIDTH" "BM.THM_CNF_DISC_IMG_HEIGHT" "0" "1000" "1" "0" "1000" "2"

    ADDWIDGET "LABEL" "$BM.TXT_THEME_COLOR$"
    ADDWIDGET "COLOR" "  $BM.TXT_THEME_BG_COLOR$" "$BM.TXT_HINT_THEME_BG_COLOR$" "BM.THM_CNF_BG_COLOR"
    ADDWIDGET "COLOR" "  $BM.TXT_THEME_FONT_COLOR$" "$BM.TXT_HINT_THEME_FONT_COLOR$" "BM.THM_CNF_FONT_COLOR_SELECTION"
    ADDWIDGET "COLOR" "  $BM.TXT_THEME_FONT_COLOR_LABEL$" "$BM.TXT_HINT_THEME_FONT_COLOR_LABEL$" "BM.THM_CNF_FONT_COLOR_LABEL"
    ADDWIDGET "COLOR" "  $BM.TXT_THEME_FONT_COLOR_SCROLLER$" "$BM.TXT_HINT_THEME_FONT_COLOR_SCROLLER$" "BM.THM_CNF_FONT_COLOR_SCROLLER"

    ADDWIDGET "LABEL" ""
    ADDWIDGET "CALL" "$BM.TXT_BACK$" "$BM.TXT_HINT_BACK$" "$ARG0$" "PSEUDO"
    RETURN 0

:SET_THEME
    CLEARWIDGETS
    SKIPBACK
    SET "BM.CNF_THEME_SCRIPT" "$ARG4$"
    LOADEXEC "PBAT" "$ARG4$" "LOAD"
    SET "BM.CONFIG_CHANGED" "1"
    SET "BM.THM_CONF_CHANGED" "1"
    EXIT 0

:MENU_CHOOSE_DEV
    CLEARWIDGETS
    EVAL SETTITLE "$$BM.TXT_$ARG2$_$ARG3$_FROM$$"
    ADDWIDGET "LABEL" "$BM.TXT_DEV$"
    LOADEXEC "PBATS" "$BM.BM_PATH$/DEVS/*/DEVINFO.PBT" "IF_QUERY" "^"$ARG0$^" ^"MENU_CHOOSE_COMP^" ^"$ARG2$^" ^"$ARG3$^" ^"$ARG4$^"" "" "$ARG2$"
    RETURN 0

:MENU_CHOOSE_COMP
    CLEARWIDGETS
    EVAL SETTITLE "$$BM.TXT_$ARG2$_AVAIL$$ $ARG5$"
    EVAL ADDWIDGET "LABEL" "$$BM.TXT_$ARG3$$$"
    IF EQU "$BM.CNF_APPDIR$" "1"
        ADDWIDGET "LABEL" "$ARG5$:/BM/$ARG3$S/*"
    ENDIF
    # ARG2 added at end to be used to hide apps when browsing. Is RUN, INSTALL or REMOVE (ARG3 in *INFO.PBT). Reference BM/APPS/BM/APPINFO.PBT for usage
    LOADEXEC "PBATS" "$ARG5$:/BM/$ARG3$S/*/$ARG3$INFO.PBT" "QUERY" "^"$ARG0$^" ^"$ARG4$^" ^"$ARG2$^" ^"$ARG3$^"" "$ARG2$"
    # Also look for apps on defined root folders from BM/SCRIPTS/BMRTFLDR.PBT and /APPS/*
    IF MATCHES "APP" "$ARG3$"
        IF EQU "$BM.CNF_APPDIR$" "1"
            ADDWIDGET "LABEL" "$ARG5$:/*"
        ENDIF
        LOADEXEC "PBAT" "$BM.SCRIPTS$/BMRTFLDR.PBT" "$ARG1$" "$ARG2$" "$ARG3$" "$ARG4$" "$ARG5$" "$ARG6$"
        IF NOT MATCHES "mc?" "$ARG5$"
            IF EQU "$BM.CNF_APPDIR$" "1"
                ADDWIDGET "LABEL" "$ARG5$:/APPS/*"
            ENDIF
            LOADEXEC "PBATS" "$ARG5$:/APPS/*/$ARG3$INFO.PBT" "QUERY" "^"$ARG0$^" ^"$ARG4$^" ^"$ARG2$^" ^"$ARG3$^"" "$ARG2$"
        ENDIF
    ENDIF
    RETURN 0

:ACTION
    CLEARWIDGETS
    SKIPBACK
    LOADEXEC "PBAT" "$ARG4$" "$ARG2$"
    RETURN 0

:MENU_CHOOSE_INST_DEV
    CLEARWIDGETS
    EVAL SETTITLE "$$BM.TXT_INSTALL_$ARG3$_TO$$"
    ADDWIDGET "LABEL" "$ARG8$"
    ADDWIDGET "LABEL" "$BM.TXT_FROM$"
    ADDWIDGET "LABEL" "  $ARG9$"
    ADDWIDGET "LABEL" "$BM.TXT_TO$ $BM.TXT_DEV$"
    IF GTE "$BM.CNF_REMOVE_INSTALL_CONFIRM$" "2" 
        LOADEXEC "PBATS" "$BM.BM_PATH$/DEVS/*/DEVINFO.PBT" "IF_QUERY" "^"$ARG0$^" ^"MENU_CONFIRM_YESNO^" ^"$ARG3$^" ^"$ARG4$^"" "$ARG4$" "$ARG2$" "$ARG1$" "$ARG8$" "$ARG9$" "$ARG10$" "$ARG11$"
    ELSE
        LOADEXEC "PBATS" "$BM.BM_PATH$/DEVS/*/DEVINFO.PBT" "IF_QUERY" "^"$ARG0$^" ^"INSTALL_COMP^" ^"$ARG3$^" ^"$ARG4$^"" "$ARG4$" "$ARG2$" "$ARG1$"
    ENDIF
    SKIPBACK
    RETURN 0

:MENU_CONFIRM_YESNO
    CLEARWIDGETS
    SETTITLE "$BM.TXT_CONFIRM_CHOICE$"
    SKIPBACK
    IF MATCHES "REMOVE" "$ARG2$"
        ADDWIDGET "LABEL" "$BM.TXT_MENU_REMOVE$ $ARG8$"
        ADDWIDGET "LABEL" "$BM.TXT_FROM$"
        ADDWIDGET "LABEL" "  $ARG9$ ?"
        ADDWIDGET "RETURN" "$BM.TXT_NO$" "$BM.TXT_HINT_CANCEL_CHOICE$"
        ADDWIDGET "CALL" "$BM.TXT_YES$" "$BM.TXT_HINT_CONFIRM_CHOICE$" "$ARG0$" "ACTION" "$ARG2$" "$ARG3$" "$ARG4$"
    ELSEIF MATCHES "INSTALL" "$ARG6$"
        ADDWIDGET "LABEL" "$BM.TXT_MENU_INSTALL$ $ARG8$"
        ADDWIDGET "LABEL" "$BM.TXT_FROM$"
        ADDWIDGET "LABEL" "  $ARG9$"
        ADDWIDGET "LABEL" "$BM.TXT_TO$"
        ADDWIDGET "LABEL" "  $ARG4$:/$ARG10$ ?"
        ADDWIDGET "RETURN" "$BM.TXT_NO$" "$BM.TXT_HINT_CANCEL_CHOICE$"
        ADDWIDGET "CALL" "$BM.TXT_YES$" "$BM.TXT_HINT_CONFIRM_CHOICE$" "$ARG0$" "INSTALL_COMP" "$ARG2$" "$ARG3$" "$ARG4$"
    ENDIF
    RETURN 0

:INSTALL_COMP
    CLEARWIDGETS
    LOADEXEC "PBAT" "$ARG3$" "INSTALL" "$ARG4$:/BM/$ARG2$S"
    SKIPBACK
    RETURN 0

:SET_LOAD_APP
    CLEARWIDGETS
    SKIPBACK
    SET "BM.CNF_$ARG2$_PBT" "$ARG4$"
    SET "BM.CNF_$ARG2$_PBT_ARGS" "^"RUN^""
    SET "BM.CONFIG_CHANGED" "1"
    EXIT 0

:PSEUDO
    CLEARWIDGETS
    ECHO "Back..."
    RETURN 0

:MENU_CHOOSE_LANG
    CLEARWIDGETS
    SKIPBACK
    SETTITLE "$BM.TXT_CHOOSE_LANG$"
    LOADEXEC "PBATS" "$BM.BM_PATH$/LANG/*.PBT" "QUERY" "^"$ARG0$^" ^"SET_LANG^""
    RETURN 0

:SET_LANG
    CLEARWIDGETS
    SKIPBACK
    SET "BM.CNF_LANG" "$ARG2$"
    SET "BM.CONFIG_CHANGED" "1"
    EXIT 0

:MENU_INST_LANG_DEV
    CLEARWIDGETS
    SETTITLE "$BM.TXT_SOURCE_DEV$"
    LOADEXEC "PBATS" "$BM.BM_PATH$/DEVS/*/DEVINFO.PBT" "IF_QUERY" "^"$ARG0$^" ^"MENU_INST_LANG^""
    RETURN 0

:MENU_INST_LANG
    CLEARWIDGETS
    SETTITLE "$BM.TXT_LANG_INST_AVAIL$"
    LOADEXEC "PBATS" "$ARG2$:/BM/LANG/*.PBT" "QUERY" "^"$ARG0$^" ^"INSTALL_LANG^""
    RETURN 0

:INSTALL_LANG
    CLEARWIDGETS
    LOADEXEC "PBAT" "$ARG2$" "INSTALL"
    SKIPBACK
    RETURN 0

:MENU_REMOVE_LANG
    CLEARWIDGETS
    SETTITLE "$BM.TXT_LANG_REMOVE_AVAIL$"
    LOADEXEC "PBATS" "$BM.BM_PATH$/LANG/*.PBT" "QUERY" "^"$ARG0$^" ^"REMOVE_LANG^""
    RETURN 0

:REMOVE_LANG
    CLEARWIDGETS
    SKIPBACK
    LOADEXEC "PBAT" "$ARG2$" "REMOVE"
    RETURN 0
    